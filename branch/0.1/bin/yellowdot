#!/bin/env perl

use strict;
use warnings;

use GD::Simple;
use Getopt::Long;
use Pod::Usage;

sub required
{
	my ($options, @flags) = @_;
	
	foreach my $flag (@flags)
	{
		pod2usage("'$flag' is a required option") if (!exists $options->{$flag});
	}
}

sub parse_cmdline
{
	my $options = {};	
	GetOptions ($options, "paper=s", "filename=s", "help");
		
	pod2usage(1) if $options->{help};
	required($options, "paper", "filename");
	
	return $options;
}

sub paper_size
{
	my ($paper) = @_;

	my ($max_x, $max_y);

	if (uc($paper) eq "LETTER")
	{
		($max_x, $max_y) = (2550, 3300);
	}
	elsif (uc($paper) eq "A4")
	{
		($max_x, $max_y) = (2480, 3508);
	}
	
	return ($max_x, $max_y);
}

sub generate_image
{
	my ($max_x, $max_y, $density) = @_;
	
	my $image = GD::Simple->new($max_x, $max_y);
	$image->fgcolor('yellow');

	my $dots = $max_x * $max_y * $density;

	for (my $count = 0; $count < $dots; $count++)
	{
		my ($rand_x, $rand_y) = (int(rand()*$max_x), int(rand()*$max_y));
		$image->moveTo($rand_x, $rand_y);
		$image->ellipse(1,1);
	}
	
	return $image;
}

sub main
{
	my $options = parse_cmdline();

	my $density = 0.01;

	my ($max_x, $max_y) = paper_size($options->{paper});
	my $image = generate_image($max_x, $max_y, $density);
	
	open (OUT, ">$options->{filename}");
	print OUT $image->png;
	close (OUT);
}

&main;

__END__

=head1 NAME

yellowdot - create an image file containing yellow dots

=head1 SYNOPSIS

yellowdot --paper letter --filename overlay.png

Options:

	--paper			size of paper (letter or A4)

	--filename		name of output file

=head1 DESCRIPTION

For years, commercial printers have quietly inserted yellow dots
into their output for the purpose of tracking which printer
was used to create any document.  The purpose of the "yellowdot" 
software is simply to bring attention to the issue, and to thwart
amateur attempts to use these yellow dots to breach privacy.

This software will create a PNG containing yellow dots that can 
then be overlaid onto existing printer output.  These dots are 
randomly distributed across the image, and are therefore not 
useful for securely obfuscating the identity that is encoded 
within the printer's generated dot pattern.  However, "yellowdot"
raises the amount of resources that must be expended to 
successfully identify the printer that generated a given document.
It is hoped that "yellowdot" can be used to thwart amateur 
attempts to breach privacy, such as could conceivably be 
practiced in an office environment.  

The commercial printer yellow dot initiative has been endorsed 
in some capacity by the United States Secret Service for the 
purpose of thwarting currency counterfeiting.  As of October 2008, 
there is no law that requires printers to generate yellow dots for
any purpose, including identification purposes.  This software is 
not intended to be used for committing fraud, and it is the belief 
of the author that this software, in fact, cannot be used for such
purposes.  By analyzing a sufficient sample of printer output, the
"noise" that is generated by this program can be eliminated, and 
the original printer source can be reconstructed.

=head1 OPTIONS

=over 4

=item --paper

	Two paper sizes are currently supported: letter and A4.  Only
	300 DPI is supported, so the paper size option results in the 
	creation of a bitmap that will fill a complete side of the
	indicated paper type, when printed at 300 DPI.

=item --filename
	
	The output will be stored in a file with this name.  It 
	should end in .png or .PNG, since that is the only format 
	supported now.  However, using another extension won't 
	ruin anything.

=item --density		

	not implemented yet

=item --margin		

	not implemented yet

=back

=head1 DOWNLOAD

L<http://iandennismiller.com/software/yellowdot/yellowdot>

=head1 INSTALLATION

chmod 755 yellowdot

sudo mv yellowdot /usr/local/bin

sudo cpan -i GD

=head1 SEE ALSO

L<http://w2.eff.org/Privacy/printers/docucolor/>

L<http://frotz.zork.net/pipermail/printers/>

L<http://frotz.zork.net/pipermail/printers/2007-April/000098.html>

L<http://en.wikipedia.org/wiki/Printer_steganography>

L<http://www.washingtonpost.com/wp-dyn/content/article/2005/10/18/AR2005101801663.html>

=head1 AUTHOR

Ian Dennis Miller <iandennismiller@gmail.com>

L<http://iandennismiller.com/software/yellowdot>

=head1 COPYRIGHT

Copyright (c) 2008 Ian Dennis Miller

=head1 LICENSE

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.
Except as contained in this notice, the name(s) of the above 
copyright holders shall not be used in advertising or otherwise 
to promote the sale, use or other dealings in this Software 
without prior written authorization.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

=cut

1;